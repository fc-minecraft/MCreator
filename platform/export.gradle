import javax.inject.Inject

interface ExecCtx {
    @Inject ExecOperations getExecOps()
}

ext.execCtx = objects.newInstance(ExecCtx)

if (project.snapshot) {
    project.mcreatorconf.setProperty('mcreator', "EAP " + (String) project.mcreatorconf.getProperty('mcreator') + "." + (String) project.builddate)
}

tasks.register('cleanupPlugins') {
    doFirst {
        delete 'build/plugins/'
    }
}

// export  all plugins
file('plugins').traverse([maxDepth: 0, type: groovy.io.FileType.DIRECTORIES]) {
    tasks.register('export_plugin_' + it.getName().replace('.', ''), Zip.class) { task ->
        task.archiveFileName = it.getName() + '.zip'
        task.destinationDirectory = file('build/plugins/')
        task.from 'plugins/' + it.getName()
        task.group = "mcreatorplugins"
        task.mustRunAfter cleanupPlugins
    }
}

tasks.register('exportPlugins') {
    dependsOn tasks.matching { Task t -> t.name.startsWith("export_plugin_") }
    dependsOn cleanupPlugins
}

jar {
    doFirst {
        delete 'build/export/'
        mkdir 'build/export/'
    }

    archiveFileName = 'mcreator.jar'
    manifest {
        attributes(
                'Main-Class': 'net.mcreator.Launcher',
                'MCreator-Version': project.mcreatorconf.getProperty('mcreator'),
                'Build-Date': project.builddate,
                'Build-Is-Snapshot': project.snapshot
        )
    }
}

tasks.register('generateExportReport') {
    doLast {
        def report = ""

        def fg = new HashSet<String>()
        def nf = new HashSet<String>()
        def bd = new HashSet<String>()
        def dp = new HashSet<String>()
        def rp = new HashSet<String>()
        file('plugins').traverse([maxDepth: 1, type: groovy.io.FileType.DIRECTORIES]) {
            if (it.getPath().contains("mcreator-link"))
                return // do not include mcreator-link generator folders in version checks

            def fn = it.getName()
            if (fn.startsWith("addon-")) {
                bd.add(fn.substring(fn.lastIndexOf("-") + 1))
            } else if (fn.startsWith("datapack-")) {
                dp.add(fn.substring(fn.lastIndexOf("-") + 1))
            } else if (fn.startsWith("forge-")) {
                fg.add(fn.substring(fn.lastIndexOf("-") + 1))
            } else if (fn.startsWith("neoforge-")) {
                nf.add(fn.substring(fn.lastIndexOf("-") + 1))
            } else if (fn.startsWith("resourcepack-")) {
                rp.add(fn.substring(fn.lastIndexOf("-") + 1))
            }
        }

        fg = new ArrayList<>(fg)
        nf = new ArrayList<>(nf)
        bd = new ArrayList<>(bd)
        dp = new ArrayList<>(dp)
        rp = new ArrayList<>(rp)
        fg.sort { a, b -> b <=> a }
        nf.sort { a, b -> b <=> a }
        bd.sort { a, b -> b <=> a }
        dp.sort { a, b -> b <=> a }
        rp.sort { a, b -> b <=> a }

        def generators = new ArrayList<String>()
        if (!nf.isEmpty()) generators.add("NeoForge " + String.join("/", nf))
        if (!fg.isEmpty()) generators.add("Minecraft Forge " + String.join("/", fg))
        if (!dp.isEmpty()) generators.add("Data Packs " + String.join("/", dp))
        if (!rp.isEmpty()) generators.add("Resource Packs " + String.join("/", rp))
        if (!bd.isEmpty()) generators.add("and Bedrock Edition " + String.join("/", bd))

        report += 'mcreator=' + version.toString() + '.' + project.builddate.toString() + '\n'
        report += 'version=' + (String) project.mcreatorconf.getProperty('mcreator').toString() + '\n'
        report += 'builddate=' + project.builddate.toString() + '\n'
        report += 'minecraft=' + String.join(", ", generators) + '\n'

        file('build').mkdirs()
        file('build/reports').mkdirs()
        file("build/reports/export.txt").write(report)
    }
}

tasks.register('exportAll') {
    group = 'export'

    dependsOn exportWindows
    dependsOn exportMac
    dependsOn exportLinux
    dependsOn generateExportReport
}

tasks.register('obfuscateJar', JavaExec) {
    classpath = configurations.proguard
    mainClass.set('proguard.ProGuard')
    
    doFirst {
        def inFile = file('build/libs/mcreator.jar')
        def outFile = file("build/tmp/mcreator-obf.jar")
        outFile.parentFile.mkdirs()
        
        def libs = (configurations.compileClasspath + configurations.runtimeClasspath).findAll { it.exists() && it.isFile() }.collect { it.absolutePath }.unique()
        
        def javaHome = System.getProperty('java.home')
        def argsList = [
            '@proguard.txt',
            '-injars', inFile.absolutePath,
            '-outjars', outFile.absolutePath
        ]
        
        // Robust Java Runtime detection for ProGuard 7+
        def addedJmods = false
        ["${javaHome}", "${javaHome}/..", "${javaHome}/../../"].each { path ->
            def dir = file("${path}/jmods")
            if (dir.exists() && !addedJmods) {
                dir.listFiles()?.findAll { it.name.endsWith('.jmod') }?.each { 
                    argsList.add('-libraryjars')
                    argsList.add(it.absolutePath + "(!**.jar;!module-info.class)")
                }
                addedJmods = true
            }
        }
        
        if (!addedJmods) {
            argsList.add('-libraryjars')
            argsList.add(javaHome)
        }
        
        libs.each { argsList.add('-libraryjars'); argsList.add(it) }
        
        args = argsList
    }
    
    doLast {
        copy {
            from file("build/tmp/mcreator-obf.jar")
            into file('build/libs')
            rename { 'mcreator.jar' }
        }
    }
}

if (project.hasProperty('obfuscate')) {
    jar.finalizedBy 'obfuscateJar'
}

apply from: 'platform/windows/windows.gradle'
apply from: 'platform/linux/linux.gradle'
apply from: 'platform/mac/mac.gradle'